#!/bin/sh
# the next line restarts using wish \
exec tclsh "$0" ${1+"$@"}

#
# Copyright (c) by Peter De Rijk (VIB - University of Antwerp)
# See the file "license.txt" for information on usage and redistribution of
# this file, and for a DISCLAIMER OF ALL WARRANTIES.
#

# see where we are, add the lib dir in the original location to auto_path, load extension
set script [file join [pwd] [info script]]
set scriptname [file tail $script]
while 1 {
	if {[catch {set script [file join [pwd] [file readlink $script]]}]} break
}

set appdir [file dir $script]
if {[lrange [file split $script] end-2 end] eq {apps cg cg.tcl}} {
	set appbasedir [file dir [file dir $appdir]]
	set auto_path [list $appbasedir/lib $appbasedir/lib/tcl8.5 $appbasedir/lib/tk8.5]
}
lappend auto_path $appdir/lib $appdir/lib-exp
source $appdir/lib/file.tcl
package require Extral
package require genomecomb
genomecombenv

# generic help on no args
if {[llength $argv] < 1} {
	cg_help
}

proc parse_generic_args {subcmd argslist} {
	if {[llength $argslist] == 1 && [inlist {-h -help --help} $argslist]} {
		if {[catch {help $subcmd} msg]} {
			puts stderr $msg
			exit 1
		}
		exit 0
	}
	set ::stacktraceonerror 0
	set remove {}
	set pos 0
	foreach {key value} $argslist {
		if {[string index $key 0] ne "-"} {
			break
		} elseif {$key eq "--"} {
			break
		} elseif {$key in "--verbose -verbose -v"} {
			if {![isint $value]} {error "$value is not a number, only numbers are accepted as value for -v (--verbose)"}
			logverbose $value
			lappend remove $pos [expr {$pos + 1}]
		} elseif {$key in "--stack -stack"} {
			if {$value in {0 1}} {
				set ::stacktraceonerror $value
				lappend remove $pos [expr {$pos + 1}]
			} else {
				error "$value is not 0 or 1, the only accepted values for --stack"
			}
		} elseif {$key in "--compressionlevel -compressionlevel"} {
			setdefcompressionlevel $value
			lappend remove $pos [expr {$pos + 1}]
		} else {
			break
		}
		incr pos 2
	}
	if {[llength $remove]} {set argslist [list_sub $argslist -exclude $remove]}
	return $argslist
}

set action [list_shift argv]
set args $argv
unset -nocomplain argv
if {[auto_load cg_$action]} {
	set args [parse_generic_args $action $args]
	set ::base "cg $action"
	if {[catch {cg_$action {*}$args} msg]} {
		if {!$stacktraceonerror} {
			puts stderr $msg
			exit 1
		} else {
			puts stderr "$::errorInfo"
			exit 1
		}
	}
} elseif {[file executable $::appdir/plugins/cg_$action]} {
	if {[llength $args] == 1 && [inlist {-h --help} $args]} {
		help $action
		exit 0
	}
	exec $::appdir/plugins/cg_$action {*}$args <@ stdin >@ stdout 2>@ stderr
} else {
	puts stderr "ERROR: unkown action $action"
	puts stderr "must be one of: [join [help_actions] ", "], ..."
	puts stderr "For more help use:\ncg help"
	exit 1
}
